plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// groupid and versioning
group 'dev.spaceseries'

project.ext.majorVersion = '1'
project.ext.midVersion = '0'
project.ext.minorVersion = '0'
project.ext.versionType = 'SNAPSHOT'
project.ext.version = project.ext.majorVersion + '.' + project.ext.midVersion + '.' + project.ext.minorVersion + '-' + project.ext.versionType;
version = project.ext.version

// repos
repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

// dependencies
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // spigot
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    // bungee
    compileOnly "net.md-5:bungeecord-api:1.16-R0.4-SNAPSHOT"

    // redis
    implementation group: 'redis.clients', name: 'jedis', version: '2.9.0'

    // hikari
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.1.0'

    // snakeYAML
    // https://mvnrepository.com/artifact/org.yaml/snakeyaml
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.8'

    // google gson
    compileOnly 'com.google.code.gson:gson:2.8.6'

    // adventure
    // platform
    implementation "net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bungeecord:4.0.0-SNAPSHOT"
    // minmessage
    implementation 'net.kyori:adventure-text-minimessage:4.0.0-SNAPSHOT'
    // apis
    implementation "net.kyori:adventure-api:4.3.0"
    implementation "net.kyori:adventure-text-serializer-plain:4.3.0"
    implementation "net.kyori:adventure-text-serializer-gson:4.3.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.3.0"

}

// shadow jar task (shades into jar)
shadowJar {
    archiveName = "spaceapi-${version}.jar"
}


// builds actual libaries into jar
jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// artifacts
artifacts {
    archives shadowJar
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

// javadoc
javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// sources jar
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

// publishing task
publishing {
    repositories {
        maven {
            def releasesRepoUrl = "http://repo.yakovliam.com/releases"
            def snapshotsRepoUrl = "http://repo.yakovliam.com/snapshots"
            url = project.ext.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username repoUsername
                password repoPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

